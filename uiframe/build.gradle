apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'net.saliman.cobertura'
apply from: '../maven_push.gradle'
apply from: '../config/quality.gradle'
apply plugin: 'com.jakewharton.hugo'

dependencies {
    implementation 'mobi.cangol.mobile:appcore:1.2.5'
    implementation 'mobi.cangol.mobile:actionbar:1.2.3'
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.slidingpanelayout:slidingpanelayout:1.1.0'
}
android {
    compileSdkVersion Integer.decode("$sdk_version")
    buildToolsVersion "$build_version"

    defaultConfig {
        minSdkVersion Integer.decode("$min_version")
        targetSdkVersion Integer.decode("$target_version")
        multiDexEnabled true
        versionCode Integer.decode("${POM_VERSION_CODE}")
        versionName "${POM_VERSION}"
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        encoding "UTF-8"
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
    destinationDir = file("../docs/")
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
cobertura.coverageFormats = ['html', 'xml']

tasks.coveralls {
    dependsOn 'check'
}